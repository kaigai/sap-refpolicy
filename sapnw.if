## <summary>Policy for SAP NetWeaver</summary>

########################################
## <summary>
##	Execute SAP NW Scripts in the sapnw_script_t domain
## </summary>
## <param name="domain">
##      <summary>
##      Domain allowed to transition.
##      </summary>
## </param>
#
interface(`sapnw_domtrans_script',`
	gen_require(`
		type sapnw_script_t, sapnw_script_exec_t;
	')

	corecmd_search_bin($1)
	domtrans_pattern($1, sapnw_script_exec_t, sapnw_script_t)
')



########################################
## <summary>
##	Allow a domain to create shared memory segment
##	to communicate with SAP NetWeaver.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`sapnw_create_shared_mem',`
	gen_require(`
		attribute sapnw_shared_mem_type;
	')

	typeattribute $1 sapnw_shared_mem_type;
	allow $1 sapnw_shared_mem_type:shm create_shm_perms;
')

########################################
## <summary>
##	Allow a domain to create shared memory segment
##	to communicate with SAP NetWeaver.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`sapnw_rw_shared_mem',`
	gen_require(`
		attribute sapnw_shared_mem_type;
	')
	allow $1 sapnw_shared_mem_type:shm rw_shm_perms;
')

########################################
## <summary>
##	Allow a domain to create tmpfs entries corresponding to
##	SAP NetWeaver.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`sapnw_create_tmpfs_files',`
	gen_require(`
		type sapnw_tmpfs_t;
	')

	fs_getattr_tmpfs($1)
	fs_search_tmpfs($1)
	fs_tmpfs_filetrans($1, sapnw_tmpfs_t, file)
	manage_files_pattern($1, sapnw_tmpfs_t, sapnw_tmpfs_t)
	mmap_files_pattern($1, sapnw_tmpfs_t, sapnw_tmpfs_t)
')

########################################
## <summary>
##	Allow a domain to create tmp files associated with
##	SAP NetWeaver.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`sapnw_create_tmp_files',`
	gen_require(`
		type sapnw_tmp_t;
	')
	files_tmp_filetrans($1, sapnw_tmp_t, {file dir sock_file})
	manage_dirs_pattern($1, sapnw_tmp_t, sapnw_tmp_t)
	manage_files_pattern($1, sapnw_tmp_t, sapnw_tmp_t)
	manage_sock_files_pattern($1, sapnw_tmp_t, sapnw_tmp_t)
')

########################################
## <summary>
##	Allow a domain to create working files associated with
##	SAP NetWeaver.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`sapnw_create_work_files',`
	gen_require(`
		type sapnw_work_t;
	')
	manage_dirs_pattern($1, sapnw_work_t, sapnw_work_t)
	manage_files_pattern($1, sapnw_work_t, sapnw_work_t)
	manage_lnk_files_pattern($1, sapnw_work_t, sapnw_work_t)
	manage_sock_files_pattern($1, sapnw_work_t, sapnw_work_t)
')

interface(`sapnw_read_var_run',`
	gen_require(`
		type sapnw_var_run_t;
	')
	read_files_pattern($1, var_run_t, sapnw_var_run_t)
')

########################################
## <summary>
##	Allow a domain to execute commands of SAP NetWeaver
##	without domain transition.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`sapnw_exec',`
	gen_require(`
		type sapnw_exec_t;
	')
	corecmd_search_bin($1)
	can_exec($1, sapnw_exec_t)
')

########################################
## <summary>
##	Allow the specified domain to connect to SAP NetWeaver with a tcp socket.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`sapnw_tcp_connect',`
	gen_require(`
		type sapnw_t;
	')

	corenet_tcp_recvfrom_labeled($1, sapnw_t)
	corenet_tcp_sendrecv_sapnw_port($1)
	corenet_tcp_connect_sapnw_port($1)
	corenet_sendrecv_sapnw_client_packets($1)
')

########################################
## <summary>
##      Allow the specified domain to connect to postgresql with a unix socket.
## </summary>
## <param name="domain">
##      <summary>
##      Domain allowed access.
##      </summary>
## </param>
## <rolecap/>
#
interface(`sapnw_stream_connect',`
	gen_require(`
		type sapnw_t, sapnw_tmp_t;
        ')
	allow $1 sapnw_t:unix_stream_socket connectto;
	allow $1 sapnw_tmp_t:sock_file write;
')

########################################
## <summary>
##	All of the rules required to administrate an SAP NetWeaver environment
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	The role to be allowed to manage the sapnw domain.
##	</summary>
## </param>
## <rolecap/>
#
interface(`sapnw_admin',`
	gen_require(`
		attribute sapnw_admin_type;
		type sapnw_t;
		type sapnw_oscol_t;
		type sapnw_initrc_exec_t;
	')
	typeattribute $1 sapnw_admin_type;

	allow $1 sapnw_t:process { ptrace signal_perms };
	allow $1 sapnw_oscol_t:process { ptrace signal_perms };
	ps_process_pattern($1, sapnw_t)
	ps_process_pattern($1, sapnw_oscol_t)

	init_labeled_script_domtrans($1, sapnw_initrc_exec_t)
	domain_system_change_exemption($1)
	role_transition $2 sapnw_initrc_exec_t system_r;
	allow $2 system_r;

	sapnw_create_shared_mem($1)
	sapnw_create_tmpfs_files($1)
	sapnw_create_tmp_files($1)
	sapnw_create_work_files($1)
	sapnw_read_var_run($1)
	sapnw_exec($1)

	sapnw_stream_connect($1)
	sapnw_tcp_connect($1)
')
